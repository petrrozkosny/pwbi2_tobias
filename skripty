dim_datumy = 

-- rok,mesic den,tyden,kvartal,vikend
ADDCOLUMNS(
    SELECTCOLUMNS(
        -- funkce vraci sloupecek [Date]
        CALENDARAUTO(),
    -- nazev sloupce vraceneho funkci SELECTCOLUMNS()
    "datum"
    -- hodnota sloupce vraceneho funkci SELECTCOLUMNS()
    ,[Date])
,"rok",YEAR([datum])
,"kvartal",QUARTER([datum])
,"mesic",MONTH([datum])
,"tyden",WEEKNUM([datum],21)
,"den",DAY([datum]))

__________________________________

01 suma srazek cesko = 
VAR verze1 = 
-- TAKHLE NE
CALCULATE(
    SUM(data[Srazky]),
FILTER(dim_lokality,dim_lokality[Zeme] = "Cesko")
)
-- TAKTO ANO, tzv. booleanovsky zapis
VAR verze2 =
CALCULATE(
    SUM(data[Srazky]),
    dim_lokality[Zeme] = "Cesko")

-- TAKTO ANO, pokud nefunguje verze2
VAR verze3 =
CALCULATE(
    SUM(data[Srazky]),
    FILTER(
        ALL(dim_lokality[Zeme]),
    dim_lokality[Zeme] = "Cesko"))
return verze3
___________________________________________

02 suma srazek cesko mesic 7 = 
CALCULATE(
    SUM(data[Srazky]),
dim_lokality[Zeme] = "Cesko",
dim_datumy[mesic] = 7)
________________________________________

03 suma srazek cele obdobi = 

CALCULATE(
    SUM(data[Srazky]),
    -- Funkce ALL(), pokud je argumentem sloupec ci sloupce, tak rusi
    -- pouze filtry nad danymi sloupci, tj. ne tzv. crossfiltry
    ALL(dim_datumy[rok]))

______________________________________________

04 suma srazek cesko predchozi rok = 

CALCULATE(
    SUM(data[Srazky]),
    dim_lokality[Zeme] = "Cesko",
    dim_datumy[rok] = MAX(dim_datumy[rok]) - 1)
_________________________________________

05 posledni datum maximalnich srazek = 
-- v dane lokalite, roce
CALCULATE(
    MAX(data[Datum]),
    data[Srazky] = MAX(data[Srazky])
)
____________________________________________

06 nejteplejsi rok = 

VAR max_teplota_rok_lokalita = MAX(data[Teplota])
-- bez ohledu na rok
VAR max_teplota_lokalita = CALCULATE(MAX(data[Teplota]),ALL(dim_datumy[rok]))

RETURN IF(max_teplota_lokalita = max_teplota_rok_lokalita,"ANO","NE")

____________________________________________________


07 poce tropickych dnu = 

-- pocet datumu, kdy teplota >=30
-- v dane lokalite, roce

CALCULATE(
    COUNT(data[Datum]),
data[Teplota] >=30)

_______________________________________________________________________
08 maxilani teplota o prazdninach = 

-- tj. v dane lokalite, roce, mesicich 7 a 8

CALCULATE(
    MAX(data[Teplota]),
dim_datumy[mesic] IN {7,8})
_________________________________________________________________________

09 suma srazek mezirocne = 

-- suma srazek v dane lokalite, roce
SUM(data[srazky])
    -
-- suma srazek v dane lokalite, predchozi rok
CALCULATE(
    SUM(data[Srazky]),
    dim_datumy[rok] = MAX(dim_datumy[rok])-1)

______________________________________________________________________________

10 nazev tabulky = 

SWITCH(TRUE(),
    HASONEFILTER(dim_lokality[Lokalita]),
"Vývoj maximální teploty v lokalitě "&SELECTEDVALUE(dim_lokality[Lokalita]),
    ISFILTERED(dim_lokality[Lokalita]),
"Vývoj maximální teploty v lokalitách "&
    CONCATENATEX(dim_lokality,dim_lokality[Lokalita],", "),
"Vývoj maximální teploty napříč lokalitami"
)
_________________________________________________________________________________

11 pf maximalni teplota = 

-- maximalni teplota v danem mesici, roce, lokalite
VAR max_teplota = MAX(data[Teplota])

-- maximalni teplota v danem mesici, lokalite, tj. bez ohledu na rok
VAR max_teplota_mesic = 
    CALCULATE(
        MAX(data[Teplota]),
        ALL(dim_datumy[rok]))

RETURN
IF(max_teplota = max_teplota_mesic,"GOLD")

______________________________________________________________________________________

12 maximalni teplota lokalita = 
MAX(data[Teplota])

__________________________________________________________________________________
13 pocet vyskytu max teplota = 
-- v dane lokalite = 
CALCULATE(
    COUNT(data[Datum]),
data[Teplota] = MAX(data[Teplota]))

____________________________________________________________________________

14 posledni vyskyt max teplota = 
-- v dane lokalite = 
CALCULATE(
    max(data[Datum]),
data[Teplota] = MAX(data[Teplota]))
_____________________________________________________________________________

15 text karty = 
"Maximální teplota "&[12 maximalni teplota lokalita] &" naměřena "&[13 pocet vyskytu max teplota] &" (naposledy "&[14 posledni vyskyt max teplota]&")"

_______________________________________________________________________________

agregovana tabulka = 
ADDCOLUMNS(
ALL(dim_datumy[rok]),
"suma srazek",
SUM(data[Srazky]),
"suma srazek calculate",
CALCULATE(SUM(data[Srazky])),
"suma srazek mesic 7 calculate",
CALCULATE(SUM(data[Srazky]),dim_datumy[mesic] = 7))

___________________________________________________________________________________________

21 pf max a min maximalni teplota = 

 VAR _tabulka = 
 ADDCOLUMNS(
 ALL(dim_datumy[rok]),
 "maximalni teplota",
 CALCULATE(MAX(data[Teplota])))

 VAR _min_teplota  = MINX(_tabulka,[maximalni teplota])
 VAR _max_teplota = MAXX(_tabulka,[maximalni teplota])

 RETURN
 SWITCH(TRUE(),
 MAX(data[Teplota]) = _min_teplota, "CYAN",
 MAX(data[Teplota]) = _max_teplota, "TOMATO")

________________________________________________________________________________________

23 pf topn hodnot = 

/*
Ucelem je podmine formatovat TOPN mesicu v danem roce, pricemz N dosazuje uzivatel pres vyber
na prurezu.
*/
-- tabulka se sloupci mesic, suma srazek
VAR _tabulka = 
ADDCOLUMNS(
    -- tabulka se vsemi mesici v danem roce
    ALL(dim_datumy[mesic]),
-- nazev nove pridavaneho sloupce
"suma srazek",
-- hodnota nove pridavaneho sloupce, tj. suma srazek pro dany mesic (mame all(mesic))
-- a rok, protoze filtru nad rokem jsme se nezbavili
CALCULATE(SUM(data[Srazky])))

-- tabulka se sloupci mesic, suma srazek, rank
VAR _tabulka_rank = 
-- tabulku vracenou funkci addcolumns() filtrujeme na mesic vizualu, zajima nas,
-- rank daneho mesice v roce
FILTER(
-- do tabulky z promenne _tabulka pridavame sloupecek rank
ADDCOLUMNS(
    _tabulka,
"rank",
-- pocitame rank pro hodnoty ze sloupce [suma srazek]
RANKX(_tabulka,[suma srazek],,DESC,Dense)),
dim_datumy[mesic] = SELECTEDVALUE(dim_datumy[mesic]))

RETURN
IF(
    -- pocitame rank mesice jako skalarni hodnotu, mohli bychom pouzit
    -- maxx,minx,averagex apod.
    MAXX(_tabulka_rank,[rank])
    -- mira vracejici uzivatelem vybranou hodnotu
    <='topn'[Hodnota parametru topn],
    -- barva vracena v pripade splneni podminky
    "CYAN")

________________________________________________________

24 topn lokalit = 
VAR _tabulka = 
-- tabulka se sloupci lokalita, suma srazek
ADDCOLUMNS(
    -- tabulka se vsemi lokalitami
    ALL(dim_lokality[Lokalita]),
"suma srazek",
-- suma srazek pro danou lokalitu ve vybranem roce (do tabulky data nam propada filtr z prurezu)
CALCULATE(SUM(data[Srazky])))

-- tabulka se sloupci lokalita, suma srazek, rank
VAR _tabulka_rank = 
ADDCOLUMNS(
    _tabulka,
"rank",
-- pridavame novy sloupec pocitajici rank sumy srazek v dane lokalite v danem roce
RANKX(_tabulka,[suma srazek],,DESC,Dense))
-- tabulka se sloupci lokalita, suma srazek, rank, typ lokality
VAR _tabulka_rank_typ_lokality =
ADDCOLUMNS(
    _tabulka_rank,
"typ lokality",
-- pokud je rank srazek <=3, tak vracime nazev lokality, jinak hodnotu OSTATNI
-- timto odlisujeme TOP3 a zbytek
if([rank] <=3,dim_lokality[Lokalita],"OSTATNI"))
RETURN 
IF(
    -- pokud je vybranou lokalitou na vizualu "OSTATNI",
    -- pocitame z tabulky v promenne _tabulka_rank_typ_lokality
    -- sumu srazek za lokality OSTATNI
    SELECTEDVALUE('dim typy_lokalit'[Lokalita]) = "OSTATNI",
    SUMX(
        FILTER(_tabulka_rank_typ_lokality,
        [typ lokality] = "OSTATNI"),
    [suma srazek]),
    -- pokud neni vybranou lokalitou na vizualu OSTATNI,
    -- pocitame sumu srazek z tabulky _tabulka_rank_typ_lokality pro vybranou lokalitu,
    -- napr. MOSNOV, PIDING apod.
    SUMX(
        FILTER(_tabulka_rank_typ_lokality,
        [typ lokality] = SELECTEDVALUE('dim typy_lokalit'[Lokalita])),
    [suma srazek]))
